apply plugin: 'maven-publish'
apply plugin: 'signing'

afterEvaluate {
    publishing {
        publications {
            arPublication(MavenPublication) {
                artifact(libJar)
                artifact(libSrc)
                artifact(libMd5)
                pom {
                    groupId = "org.p5android"
                    artifactId = "${libName}"
                    version = "${libVersion}"
                    packaging = "jar"
                    licenses {
                        license {
                            name = "GNU Lesser General Public License, version 2.1"
                            url = "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
                            distribution = "repo"
                        }
                    }                    
                    scm {
                        connection = 'scm:git:github.com/processing/processing-android.git'
                        developerConnection = 'scm:git:ssh://github.com/processing/processing-android.git'
                        url = 'https://github.com/processing/processing-android/tree/master'
                    }
                }        
                pom.withXml {
                    def dependenciesNode = asNode().appendNode('dependencies')                    
                    libDependencies.each { 
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        if (it.name == 'android') {
                            dependencyNode.appendNode('artifactId', 'android')
                            dependencyNode.appendNode('scope', 'runtime')
                        } else {
                            dependencyNode.appendNode('groupId', it.group)
                            dependencyNode.appendNode('artifactId', it.name)
                            dependencyNode.appendNode('version', it.version)
                            dependencyNode.appendNode('scope', 'implementation')
                        }
                    }
                }
            }
        }
    }
}

ext["signing.keyId"] = rootProject.ext["signing.keyId"]
ext["signing.password"] = rootProject.ext["signing.password"]
ext["signing.secretKeyRingFile"] = rootProject.ext["signing.secretKeyRingFile"]

signing {
    sign publishing.publications
}