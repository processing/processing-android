import com.android.build.gradle.internal.dependency.ExtractAarTransform
import com.android.build.gradle.internal.dependency.AarTransform
import com.android.build.gradle.internal.publishing.AndroidArtifacts
import com.android.build.gradle.internal.publishing.AndroidArtifacts.ArtifactType
import com.google.common.collect.ImmutableList
import org.gradle.api.artifacts.transform.ArtifactTransform
import org.gradle.api.artifacts.type.ArtifactTypeDefinition

import static org.gradle.api.internal.artifacts.ArtifactAttributes.ARTIFACT_FORMAT

import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING;

apply plugin: 'maven'

/**
 * Custom aar configuration needed to use aar files as dependencies in a pure java 
 * library project, lifted from the following repo:
 * https://github.com/nekocode/Gradle-Import-Aar
 */
configurations {
    aar {
        attributes {
            attribute(ARTIFACT_FORMAT, ArtifactTypeDefinition.JAR_TYPE)
        }

        // Add the aar inner jars to the compileClasspath
        sourceSets.main.compileClasspath += it

        // Put our custom dependencies onto IDEA's PROVIDED scope
        apply plugin: 'idea'
        idea.module.scopes.PROVIDED.plus += [it]
    }
}

dependencies {
    // Transforamtions to extract the classes.jar in the aar package
    def explodedAarType = ArtifactType.EXPLODED_AAR.getType()
    registerTransform {
        from.attribute(ARTIFACT_FORMAT, AndroidArtifacts.TYPE_AAR)
        to.attribute(ARTIFACT_FORMAT, explodedAarType)
        artifactTransform(ExtractAarTransform)
    }

    registerTransform {
        from.attribute(ARTIFACT_FORMAT, explodedAarType)
        to.attribute(ARTIFACT_FORMAT, "classes.jar")
        artifactTransform(AarTransform) { params(ArtifactType.JAR) }
    }

    registerTransform {
        from.attribute(ARTIFACT_FORMAT, "classes.jar")
        to.attribute(ARTIFACT_FORMAT, ArtifactTypeDefinition.JAR_TYPE)
        artifactTransform(ClassesJarArtifactTransform)
    }

    compileOnly name: "android"

    def SUPPORT_LIBS_VERSION = '26.0.2'
    def WEAR_VERSION = '2.1.0'
    aar "com.android.support:support-v4:$SUPPORT_LIBS_VERSION"
    aar "com.google.android.support:wearable:$WEAR_VERSION"
}

/**
 * An ArtifactTransform for renaming the classes.jar
 */
class ClassesJarArtifactTransform extends ArtifactTransform {
    @Override
    List<File> transform(File file) {
        final String[] names = file.getPath().split("/")
        final String aarName = names[names.length - 4].replace(".aar", "")
        final File renamedJar = new File(getOutputDirectory(), aarName + ".jar")
        renamedJar << file.bytes
        return ImmutableList.of(renamedJar)
    }
}

task createPom {
    def SUPPORT_LIBS_VERSION = '26.0.2'
    def WEAR_VERSION = '2.1.0'

    pom {
       project {
           groupId "org.p5android"
           artifactId "processing-core"
           version "${modeVersion}"
           packaging "jar"
           licenses {
               license {
                   name "GNU Lesser General Public License, version 2.1"
                   url "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
                   distribution "repo"
               }
           }
           dependencies {
               dependency {
                   groupId "com.android.support"
                   artifactId "support-v4"
                   version "$SUPPORT_LIBS_VERSION"
                   scope "compile"
               }
               dependency {
                   groupId "com.google.android.support"
                   artifactId "wearable"
                   version "$WEAR_VERSION"
                   scope "compile"
               }               
           }
        }
    }.writeTo("dist/processing-core-${modeVersion}.pom")
}

sourceSets {
    main {
        java {
            srcDirs = ['src/']
        }
        resources {
            srcDirs = ['src/']
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

// Does not work because of Processing-specific tags in source code, such as @webref
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

artifacts {
//     archives javadocJar
    archives sourcesJar
}

jar.doLast { task ->
    ant.checksum file: task.archivePath
}

clean.doFirst {
    delete "dist"
    delete "${coreZipPath}"
}

build.doLast {
    // Copying core jar as zip inside the mode folder
    Files.copy(file("${buildDir}/libs/core.jar").toPath(),
               file("${coreZipPath}").toPath(), REPLACE_EXISTING);

    // Copying the files for release on JCentral
    File distFolder = file("dist");
    distFolder.mkdirs();
    Files.copy(file("$buildDir/libs/core.jar").toPath(),
               file("dist/processing-core-${modeVersion}.jar").toPath(), REPLACE_EXISTING);
    Files.copy(file("$buildDir/libs/core-sources.jar").toPath(),
               file("dist/processing-core-${modeVersion}-sources.jar").toPath(), REPLACE_EXISTING);
    Files.copy(file("$buildDir/libs/core.jar.MD5").toPath(),
               file("dist/processing-core-${modeVersion}.jar.md5").toPath(), REPLACE_EXISTING);
}