import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING

apply plugin: 'maven'
apply plugin: 'kotlin'
apply plugin: 'aar'

dependencies {
    implementation name: "android"

    implementationAar "com.android.support:support-v4:${supportLibsVersion}"
    implementationAar "com.google.android.support:wearable:${wearVersion}"
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
}

task createPom {
    pom {
       project {
           groupId "org.p5android"
           artifactId "processing-core"
           version "${modeVersion}"
           packaging "jar"
           licenses {
               license {
                   name "GNU Lesser General Public License, version 2.1"
                   url "https://www.gnu.org/licenses/old-licenses/lgpl-2.1.txt"
                   distribution "repo"
               }
           }
           dependencies {
               dependency {
                   groupId "com.android.support"
                   artifactId "support-v4"
                   version "${supportLibsVersion}"
                   scope "implementation"
               }
               dependency {
                   groupId "com.google.android.support"
                   artifactId "wearable"
                   version "${wearVersion}"
                   scope "implementation"
               }               
           }
        }
    }.writeTo("dist/processing-core-${modeVersion}.pom")
}

sourceSets {
    main {
        java {
            srcDirs = ["src/"]
        }
        resources {
            srcDirs = ["src/"]
        }
    }
}

task sourcesJar(type: Jar, dependsOn: classes) {
    classifier = "sources"
    from sourceSets.main.allSource
}

// Does not work because of Processing-specific tags in source code, such as @webref
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = "javadoc"
    from javadoc.destinationDir
}

artifacts {
//     archives javadocJar
    archives sourcesJar
}

jar.doLast { task ->
    ant.checksum file: task.archivePath
}

clean.doFirst {
    delete "dist"
    delete "${coreZipPath}"
}

compileJava.doFirst {
    String[] deps = ["percent.jar",
                     "recyclerview-v7.jar",
                     "support-compat.jar",
                     "support-core-ui.jar",
                     "support-core-utils.jar",
                     "support-fragment.jar",
                     "support-media-compat.jar",
                     "support-v4.jar",
                     "wearable.jar"]                    
    for (String fn : deps) {
        Files.copy(file("${rootDir}/build/libs/" + fn).toPath(),
                   file("${rootDir}/mode/mode/" + fn).toPath(), REPLACE_EXISTING)
    }
}

build.doLast {
    // Copying core jar as zip inside the mode folder
    Files.copy(file("${buildDir}/libs/core.jar").toPath(),
               file("${coreZipPath}").toPath(), REPLACE_EXISTING)

    // Copying the files for release on JCentral
    File distFolder = file("dist")
    distFolder.mkdirs()
    Files.copy(file("${buildDir}/libs/core.jar").toPath(),
               file("dist/processing-core-${modeVersion}.jar").toPath(), REPLACE_EXISTING)
    Files.copy(file("${buildDir}/libs/core-sources.jar").toPath(),
               file("dist/processing-core-${modeVersion}-sources.jar").toPath(), REPLACE_EXISTING)
    Files.copy(file("${buildDir}/libs/core.jar.MD5").toPath(),
               file("dist/processing-core-${modeVersion}.jar.md5").toPath(), REPLACE_EXISTING)
}
buildscript {
    ext.kotlin_version = '1.3.72'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}
repositories {
    mavenCentral()
}
compileKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}
compileTestKotlin {
    kotlinOptions {
        jvmTarget = "1.8"
    }
}