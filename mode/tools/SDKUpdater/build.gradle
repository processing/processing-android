import java.nio.file.Files
import static java.nio.file.StandardCopyOption.REPLACE_EXISTING;

configurations {
    implementation.extendsFrom implementationCopy
}

dependencies {
    // implementation group: "org.processing", name: "pde", version: "${processingVersion}"
    // implementation group: "org.processing", name: "java-mode", version: "${processingVersion}"
    implementation fileTree(include: ["AndroidMode.jar", "pde.jar", "JavaMode.jar"], dir: '../../mode')

    implementationCopy group: "com.android.tools", name: "sdklib", version: "${toolsLibVersion}"
    implementationCopy group: "com.android.tools", name: "repository", version: "${toolsLibVersion}"
}

// This task copies the gradle tooling jar into the mode folder
task copyToLib(type: Copy) {
  from configurations.implementationCopy.files {
    include '**/annotations-*'
    include '**/common-*'
    include '**/dvlib-*'
    include '**/gson-*'
    include '**/guava-*'
    include '**/httpcore-*'
    include '**/kotlin-stdlib-*'
    include '**/layoutlib-api-*'
    include '**/repository-*'
    include '**/sdklib-*'
  }
  into "lib"
}
build.dependsOn(copyToLib)
compileJava.dependsOn(':mode:copyToLib')

sourceSets {
    main {
        java {
            srcDirs = ["src/"]
        } 
    }
}

clean.doFirst {
    delete "tool"
    delete "lib"
}

build.doLast {
    // Copy jar file to tool folder
    File toolJar = file("tool/SDKUpdater.jar");
    toolJar.mkdirs();
    Files.copy(file("$buildDir/libs/SDKUpdater.jar").toPath(),
               toolJar.toPath(), REPLACE_EXISTING);                   
}
